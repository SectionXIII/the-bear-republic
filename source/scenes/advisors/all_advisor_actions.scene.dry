title: Actions
is-pinned-card: true
tags: advisor
view-if: 0
new-page: true

= Actions

Wels is the long-time chairman of the SPD. He is a gifted organizer and a member of the centrist faction.

- @clear_agenda
- @party_discipline
- @emergency_fundraising
- @agriculture
- @rural_campaign
- @depression
- @building_the_left
- @kpd_cooperation
- @against_toleration
- @build_reichsbanner
- @moderate_economic_plan
- @white_collar
- @union_unity
- @crisis
- @coalition
- @snap_election
- @breitscheid_international
- @breitscheid_foreign
- @against_right_and_left
- @toleration
- @finance_ministry_
- @economic_democracy_
- @welfare
- @organizing_women
- @democratic_rally
- @military_affairs
- @support_peoples_party
- @build_peoples_party
- @campaigning_peoples_party
- @organizing
- @iron_front_buildup
- @form_iron_front
- @nazi_urgency
- @womens_rights_
- @judiciary_
- @constitutional_reform_
- @left_unity
- @police_affairs
- @class_struggle
- @socialist_youth
- @media_
- @faction_support_
- @plan
- @carry_out_policy
- @root: Return to main

@clear_agenda
title: Clear the agenda
subtitle: Removes all cards from the hand.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: {!
Q.advisor_action_timer = 6;
if (Q.difficulty >= 0) {
    window.dendryUI.dendryEngine.state.currentHands['main'] = [];
} else {
    window.dendryUI.dendryEngine.state.currentHands['main.main_easy'] = [];
}
!}

All items from our agenda have been cleared.

@party_discipline
title: Party Discipline
subtitle: Reduce dissent across the party.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; left_dissent -= 5; labor_dissent -= 10; center_dissent -= 10; reformist_dissent -= 10; neorevisionist_dissent -= 5

We have enforced party discipline to reduce dissent across all factions.

@emergency_fundraising
title: Emergency Fundraising
view-if: difficulty < 1
subtitle: An emergency infusion of resources.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; resources += dues

We have gained +[+ dues +] resources.

@agriculture
title: Agricultural Policy
subtitle: Baade is the Minister of Agriculture.
choose-if: advisor_action_timer <= 0 and spd_in_government and agriculture_minister_party == "SPD"
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government : The SPD is not in government. ?][? if agriculture_minister_party != "SPD": The SPD does not have the Agriculture Ministry. ?]
on-arrival: advisor_action_timer = 6; agricultural_policy_timer -= 6
go-to: agricultural_policy


@rural_campaign
title: Rural Campaign
subtitle: Try to campaign among the rural populace.
choose-if: advisor_action_timer == 0 and rural_policy > 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if rural_policy == 0 : We have not enacted any rural policies. ?]
on-arrival: advisor_action_timer = 6; rural_spd += 5*(1-dissent); peoples_party_support += 1

We have increased our campaigning among farmers and the rural populace.

@depression
title: Addressing the economic crisis
subtitle: Formulate a plan to rescue us from the economic crisis.
choose-if: advisor_action_timer <= 0 and wtb_adopted = 0 and black_thursday_seen
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if black_thursday_seen == 0 : We are not currently in an economic crisis. ?][? if wtb_adopted > 0 : We have already adopted the WTB-Plan. ?]
on-arrival: advisor_action_timer = 6; wtb_support += 2; center_dissent += 10; labor_strength += 1

The WTB-Plan, standing for its primary supporters Woytinsky, Tarnow, and Baade, has been adopted as the unions' plan for getting us out of the depression. Baade canvasses in favor of this plan throughout the party.

@building_the_left
title: Building the Left
subtitle: Push the SPD to the left.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; left_strength += 6; left_dissent -= 8; reformist_dissent += 2

We have increased the strength of the left faction with the SPD.

@kpd_cooperation
title: Cooperation with the KPD
subtitle: Push the party to be more friendly towards the KPD.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; left_strength += 5; kpd_relation += 4*(1-dissent); reformist_dissent += 2; communist_coalition += 1 if kpd_cooperation_seen <= 1; kpd_cooperation_seen += 1

Through his work, Rosenfeld has defended individual KPD members; this should make some connections possible.

@against_toleration
title: Against toleration
view-if: not constructive_vonc
subtitle: Break our current toleration arrangement, and bring down the government via a no-confidence vote.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if spd_toleration == 0 : The SPD is not currently in toleration. ?]
choose-if: advisor_action_timer == 0 and spd_toleration
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time + 3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 8; pro_republic -= 4; new_middle_spd -= 3; rural_spd -= 3; old_middle_spd -= 3; catholics_spd -= 3; reformist_dissent += 8

We have grown tired of the burdens of toleration, and have called a vote of no confidence in the current minority government.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@build_reichsbanner
title: Reichsbanner
subtitle: Schumacher supports providing more resources to the Reichsbanner.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; reichsbanner_timer -= 6;
go-to: reichsbanner

@moderate_economic_plan
title: Support the reformist economic plan
subtitle: Call for a limited program of job creation without deficit spending.
choose-if: advisor_action_timer = 0 and moderate_plan_adopted = 0 and black_thursday_seen = 1
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if moderate_economic_plan = 0 : We have already adopted the reformist economic plan. ?] [? if black_thursday_seen == 0 : We are not currently in an economic crisis. ?]
on-arrival: advisor_action_timer = 6
go-to: crisis_program.support_moderate

@white_collar
title: Organizing white-collar workers
subtitle: Improve our standing among the new middle class.
choose-if: advisor_action_timer <= 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] 
on-arrival: advisor_action_timer = 6; new_middle_spd += 6*(1-dissent);

We have increased our organizing among white-collar workers.

@union_unity
title: Union-party coordination
subtitle: Reduce dissent among the unions.
choose-if: advisor_action_timer <= 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; labor_dissent -= 10;

We have reduced tensions with the Labor faction.

@crisis
title: Addressing the economic crisis
subtitle: Aufhäuser can help support a left-wing economic program.
choose-if: advisor_action_timer <= 0 and nationalization_adopted == 0 and black_thursday_seen == 1 and nationalization_support <= 3
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if black_thursday_seen == 0 : We are not currently in an economic crisis. ?][? if nationalization_adopted > 0 : We have already adopted the Left plan. ?]
on-arrival: advisor_action_timer = 6; 
go-to: crisis_program.support_left 

@coalition
title: Negotiating with the Coalition
subtitle: Braun can improve our relationships with our centrist and center-right coalition partners in Prussia as well as in the Reichstag.
choose-if: advisor_action_timer <= 0 and (in_republican_coalition or in_grand_coalition or in_popular_front)
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; coalition_dissent -= 1 if spd_in_government = 1 and coalition_dissent > 0; z_relation += 6; ddp_relation += 6; dvp_relation += 2; dvp_relation += 3 if in_grand_coalition

We have improved our relations with the [? if in_grand_coalition : Center (Zentrum) Party, DDP, and DVP, ?][? if not in_grand_coalition: Center (Zentrum) Party and DDP, ?] and reduced tensions with the coalition.

@snap_election
title: Snap Election
subtitle: Call a snap election.
unavailable-subtitle: [? if chancellor != "Braun" and president != "Braun" : Braun must be Chancellor or President. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if president = "Braun" and presidential_powers : We have removed this power from the president with a constitutional reform. ?]
choose-if: advisor_action_timer == 0 and (chancellor == "Braun" or (president == "Braun" and not presidential_powers))
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 

@breitscheid_international
title: International Party Relations
subtitle: Breitscheid is one of our contacts with international socialist parties.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer == 0
on-arrival: advisor_action_timer = 6; international_relations_timer -= 12;
go-to: international_relations

@breitscheid_foreign
title: Foreign Policy
subtitle: As Foreign Minister, Breitscheid can set our foreign policy.
unavailable-subtitle: [? if not spd_in_government: The SPD is not in government. ?] [? if foreign_minister_party != "SPD" : The SPD does not control the Foreign Ministry. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer == 0 and spd_in_government and foreign_minister_party == "SPD"
on-arrival: advisor_action_timer = 6; foreign_policy_timer -= 8;
go-to: foreign_policy

@against_right_and_left
title: Against Right and Left
subtitle: As a Centrist, Hilferding seeks to steer a course between the party's factions.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 5; advisor_action_time = time; center_strength += 10; left_strength -= 5; labor_strength -= 5; neorevisionist_strength -= 5; center_dissent -= 10

We have reduced the strength of the dissident factions in the party.

@toleration
title: Defending Toleration
subtitle: Hilferding supports cooperation with the bourgeois parties.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_toleration: The SPD is not in toleration. ?]
choose-if: spd_in_government = 0 and spd_toleration = 1 and advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; left_dissent -= 10; neorevisionist_dissent -= 8; center_dissent -= 5

We have reduced dissent among the anti-toleration factions.

@finance_ministry_
title: Fiscal Policy
subtitle: As Finance Minister, Hilferding can enact tax policies.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if spd_in_government = 0 : SPD not in government: ?] [? if finance_minister_party != "SPD" : The SPD does not control the finance ministry. ?]
choose-if: advisor_action_timer == 0 and spd_in_government == 1 and finance_minister_party == "SPD"
on-arrival: advisor_action_timer = 6; fiscal_policy_timer -= 6
go-to: fiscal_policy

@economic_democracy_
title: Economic Democracy
subtitle: Enact policies to build a more democratic economy.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if spd_in_government = 0 : SPD not in government: ?] [? if labor_minister_party != "SPD" or (finance_minister_party != "SPD" and economic_minister_party != "SPD") : We do not have the necessary government ministers. ?]
choose-if: advisor_action_timer == 0 and spd_in_government == 1 and (economic_minister_party == "SPD" or finance_minister_party == "SPD") and (labor_minister_party == "SPD")
on-arrival: advisor_action_timer = 6; economic_democracy_timer -= 10
go-to: economic_democracy

@welfare
title: Support Workers' Welfare, our mutual aid organization.
subtitle: -1 resources
choose-if: resources > 0 and advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if resources < 0: Not enough resources. ?]
on-arrival: resources -= 1; workers_aid += 1; unemployed_spd += 5*(1 - dissent); workers_spd += 3*(1-dissent); workers_spd += 2*(1-dissent) if black_thursday_seen; advisor_action_timer = 6;

We have increased funding to Workers' Welfare. [? if black_thursday_seen > 0 : In an age of economic crisis, this will be helpful for improving our standing among the unemployed. ?]

@organizing_women
title: Organizing women
subtitle: Juchacz's strategies are aimed at more "traditional" women.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: womens_rights += 1; workers_spd += 4*(1-dissent); rural_spd += 3*(1-dissent); old_middle_spd += 3*(1-dissent); advisor_action_timer = 6

Juchacz's strategies for organizing women are aimed at working-class housewives, and acknowledge women's traditional household roles. We argue that our economic welfare programs will improve the livelihoods of women and their families.

@democratic_rally
title: Rally for democracy
subtitle: Rally our party and the German people to defend democracy and freedom.
choose-if: advisor_action_timer == 0 
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; pro_republic += 5; democratization += 1; neorevisionist_strength += 5; reformist_strength += 3; workers_spd += 4*(1-dissent) if democratization >= 4; new_middle_spd += 3*(1-dissent) if democratization >= 4

Leber helps us to rally the people in defense of democracy.

@military_affairs
title: Military affairs
subtitle: We can set our military policy.
choose-if: advisor_action_timer == 0 and spd_in_government and reichswehr_minister_party == "SPD"
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government : The SPD is not in government. ?][? if reichswehr_minister_party != "SPD": The SPD does not have the Reichswehr ministry. ?]
on-arrival: advisor_action_timer = 6;
go-to: military_policy

@support_peoples_party
title: Build support for a People's Party
subtitle: Can we expand our party beyond the industrial working class?
view-if: peoples_party = 0
choose-if: peoples_party_support <= 4 and advisor_action_timer = 0 and peoples_party == 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if peoples_party_support > 4: There is already substantial support for a People's Party. ?]
on-arrival: advisor_action_timer = 6; peoples_party_support += 1

We are building up support for the idea of a People's Party.

@build_peoples_party
title: Building a People's Party
subtitle: Can we expand our party beyond the industrial working class?
view-if: peoples_party == 0
choose-if: peoples_party_support >= 3 and advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if peoples_party_support < 3: There is not enough support for this policy within the party. ?]
on-arrival: advisor_action_timer = 6;
go-to: peoples_party

@campaigning_peoples_party
title: Campaigning as a People's Party
subtitle: Campaigning with some of our previously nontraditional demographics.
view-if: peoples_party = 1
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6;
go-to: peoples_party_campaigning

@organizing
title: Organizing workers
subtitle: Improve our standing among the workers.
choose-if: advisor_action_timer <= 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; workers_spd += 4*(1-dissent); workers_spd += 3*(1-dissent) if wtb_adopted == 1; labor_strength += 2;

We have increased our organizing among the workers.

@iron_front_buildup
title: Build up the Iron Front
subtitle: Increase our propaganda measures to recruit more members into the Reichsbanner. 
choose-if: advisor_action_timer == 0 and iron_front_formed
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if iron_front_formed == 0 : The Iron Front has not yet been formed. ?]
on-arrival: advisor_action_timer = 6; rb_strength += 200*workers_spd/(workers_spd+workers_kpd+workers_nsdap+workers_z+workers_dnvp+workers_other);

We have recruited more members into the Reichsbanner.


@form_iron_front
title: Forming the Iron Front
choose-if: advisor_action_timer == 0 and iron_front_formed == 0 and nazi_urgency >= 2
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if iron_front_formed : The Iron Front has already been formed. ?][? if nazi_urgency < 2 : The party does not yet see the danger of the Nazis. ?]
on-arrival: advisor_action_timer = 6; rb_strength += 50; nazi_urgency += 1
go-to: confronting_grizzly.iron_front 


@nazi_urgency
title: Warning about the Nazis
subtitle: By warning the party about the Nazis, we can spur our members to action.
choose-if: advisor_action_timer == 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; nazi_urgency += 1; rb_militancy += 0.01; nsdap_workers -= 4*(1-dissent);

We have begun a propaganda campaign to warn our members away from the Nazis.

@womens_rights_
title: Women's rights
subtitle: Enact policy with regards to women's rights.
choose-if: advisor_action_timer = 0 and spd_in_government
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government : The SPD is not currently in government. ?]
on-arrival: advisor_action_timer = 6; womens_rights_timer -= 10
go-to: womens_rights

@judiciary_
title: Judicial Affairs
subtitle: Enact judicial reforms.
choose-if: advisor_action_timer = 0 and spd_in_government and justice_minister_party = "SPD"
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government : The SPD is not currently in government. ?][? if justice_minister_party != "SPD" : We do not have the Justice Ministry. ?]
on-arrival: advisor_action_timer = 6; judiciary_timer -= 6
go-to: judiciary


@constitutional_reform_
title: Constitutional Reform
subtitle: Pursue reforms of the inadequate Weimar Constitution.
choose-if: advisor_action_timer = 0 and spd_in_government and justice_minister_party = "SPD" and neorevisionism and ((in_republican_coalition) or (in_spd_majority) or (in_left_front) or (in_popular_front))
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government : The SPD is not currently in government. ?] [? if justice_minister_party != "SPD" : We do not have the Justice Ministry. ?] [? if not neorevisionism: We do not have ideological support for constitutional reform yet - new ideas are needed. ?] [? if not ((in_republican_coalition) or (in_spd_majority) or (in_left_front) or (in_popular_front)) : Our current governing coalition will not support constitutional reforms. ?]
on-arrival: advisor_action_timer = 6
go-to: constitutional_reform

@left_unity
title: Left-Center Unity
subtitle: Encourage cooperation between the left and center wings of the party.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; left_dissent -= 10; labor_dissent -= 5; center_dissent -= 10; reformist_dissent -= 10; neorevisionist_dissent -= 5

We have reduced dissent among the party's factions.

@police_affairs
title: Police affairs 
subtitle: We can set our police policy. 
choose-if: advisor_action_timer == 0 and spd_in_government and interior_minister_party == "SPD"
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government : The SPD is not in government. ?][? if interior_minister_party != "SPD": The SPD does not have the Interior ministry. ?]
on-arrival: advisor_action_timer = 6; police_timer -= 6
go-to: police

@class_struggle
title: Rally for class struggle
subtitle: The core of socialism should be the struggle of the proletariat against the bourgeoisie.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; workers_spd += 4*(1-dissent); unemployed_spd += 3*(1-dissent); workers_spd += 4*(1-dissent) if nationalization_progress > 0; unemployed_spd += 3*(1-dissent) if nationalization_progress > 0; left_strength += 5; left_dissent -= 5; old_middle_spd -= 3; new_middle_spd -= 2

Our message of class struggle appeals to the left wing of the working class, while potentially alienating the middle class. [? if nationalization_progress > 0 : Our achievements in nationalization have assisted our messaging. ?]


@socialist_youth
title: Support the Socialist Youth
subtitle: We must end our reputation as a party for old men by increasing funding to the youth organizations. -1 resources
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if resources <= 0 : Not enough resources. ?]
choose-if: advisor_action_timer = 0 and resources > 0
on-arrival: advisor_action_timer = 6; resources -= 1; left_strength += 5; left_dissent -= 3; rb_strength += 50; rb_militancy += 0.025; workers_spd += 2*(1-dissent); new_middle_spd += 1;

Seydewitz supports building up the Socialist Youth, which attracts support to the Left and increases the militancy of the Reichsbanner.

@media_
title: Media
subtitle: Manage our party's newspapers and media.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; media_timer -= 6
go-to: media

@faction_support_
title: Editorial Line
subtitle: We can change the editorial line of Vorwärts to support a particular faction.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6

Which faction should we support?

- @support_left: Left
- @support_center: Center
- @support_labor: Labor
- @support_reform: Reformists
- @support_neorev: Neorevisionists

@support_left
on-arrival: left_dissent -= 8; left_strength += 8

Vorwärts prints editorials in support of Left positions.

@support_center
on-arrival: center_dissent -= 8; center_strength += 8

Vorwärts prints editorials in support of centrist positions.

@support_labor
on-arrival: labor_dissent -= 8; labor_strength += 8

Vorwärts prints editorials in support of the policies of the trade unions.

@support_reform
on-arrival: reformist_dissent -= 8; reformist_strength += 8;

Vorwärts prints editorials in support of the reformist policies.

@support_neorev
view-if: neorevisionism
on-arrival: neorevisionist_dissent -= 8; neorevisionist_strength += 8

Vorwärts prints editorials in support of neorevisionist ideas.

@plan
title: Adopt the WTB-plan!
subtitle: Convincing the rest of the party to support the WTB-plan.
choose-if: advisor_action_timer <= 0 and wtb_adopted == 0 and wtb_support >= 3 and black_thursday_seen and (not historical_mode or ((labor_strength >= center_strength) and (labor_strength >= left_strength) and (labor_strength >= reformist_strength)))
on-arrival: advisor_action_timer = 6;
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if black_thursday_seen == 0 : We are not currently in an economic crisis. ?][? if wtb_adopted > 0 : We have already adopted the WTB-Plan. ?][? if wtb_support < 3 : There is not enough support for the WTB-Plan. ?][? if (labor_strength < center_strength or labor_strength < left_strength or labor_strength < reformist_strength) and historical_mode : The labor faction is not strong enough. ?]
go-to: crisis_program.plan_adopted

@carry_out_policy
title: Carry out the economic plan.
subtitle: If we are in government, we can carry out a public works program such as the WTB-Plan.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if wtb_adopted == 0 : We have not yet adopted an economic plan. ?][? if spd_in_government == 0 : The SPD is not in government. ?][? if works_program >= 2 : We have already started implementing the plan. ?]
choose-if: advisor_action_timer <= 0 and wtb_adopted and spd_in_government and wtb_implemented <= 1
on-arrival: advisor_action_timer = 6; economic_policy_timer -= 12
go-to: economic_policy