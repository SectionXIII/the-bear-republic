title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/sangerbund.jpg

= Media

The SPD runs a veritable media empire, with many newspapers throughout every German city and region. We could use the media for various purposes...

- @commercial
- @campaign
- @ideological
- @radio
- @radio_2
- @radio_3
- @radio_4
- @return: We should not do anything with media at the moment.
- @easy_discard

@commercial
title: Make our newspapers more commercialized by broadening their appeal beyond party supporters.
subtitle: This might upset ideological purists, but it may bring in more funds and perhaps expose the middle class to socialist views.
on-arrival: resources += 1; commercialized_media += 1; middle_spring += 4*(1 - dissent); frontier_spring += 3*(1-dissent); urban_dissent += 5; mutant_dissent += 5; frontier_dissent -= 10

Our commercialized newspapers are able to bring in more funds and reach more people. However, the urban and mutant factions make their displeasure known.

@campaign
title: The purpose of our media is to elect more Long Springers.
subtitle: Our newspapers will focus on the electoral campaign.
on-arrival: campaign_media += 1; workers_spring += 4*(1 - dissent); unemployed_spring += 2*(1-dissent);

Our campaign media is mostly reaching the workers who are already saturated with Springer propaganda, but it must be doing *something*, or at least that is what we hope.

@ideological
title: We will have vibrant ideological debate within our newspapers.
subtitle: There will be space for all factions to air their views.
on-arrival: urban_dissent -= 6; frontier_dissent -= 6; mutant_dissent -= 6; intelligentsia_dissentt -= 6; old_worlder_dissent -= 6

This has been our media course historically, and it has successfully paved over the many tensions in our party.

@radio
title: Why not fund a radio station?
subtitle: -2 resources
choose-if: resources >= 2
unavailable-subtitle: Not enough resources.
view-if: radio = 0
on-arrival: radio += 1; resources -= 2; middle_spring += 4*(1 - dissent); military_spring += 3*(1 - dissent); workers_spring += 2*(1 - dissent); intelligentsia_dissent += 5; pro_republic += 3*(1-dissent) if democratization > 1

Our new radio station allows us to connect to people in a more intimate setting than before, spreading the message of Springer propaganda along with music and other popular content. Since most people cannot afford family radios right now, we are reaching the middle class, a fact of which our labor comrades are painfully aware.

@radio_2
title: We must keep on building up our radio network.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: radio > 0 and radio <= 3
on-arrival: radio += 1; resources -= 1; middle_spring += 5*(1 - dissent); military_spring += 3*(1 - dissent); workers_spring += 3*(1 - dissent); pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

Our new radio network has a mix of popular and political programming, and is proving a success.

@radio_3
title: We must keep on building up our radio network.
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: radio > 3 and radio <= 5 and year <= 2256
on-arrival: radio += 1; middle_spring += 5*(1 - dissent); military_spring += 4*(1 - dissent); workers_spring += 4*(1 - dissent); rural_spring += 3*(1 - dissent); unemployed_spring += 2*(1-dissent); pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

Our radio network has become a successful tool for spreading our message.

@radio_4
title: We should keep on supporting our radio network.
subtitle: As radio becomes more common, the novelty is beginning to wear off, and radio is becoming saturated in society. -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: radio > 3 and (radio >= 6 or year >= 2260) and radio <= 9
on-arrival: radio += 1; resources -= 1; middle_spring += 3*(1 - dissent); military_spring += 2*(1 - dissent); workers_spring += 3*(1 - dissent); rural_spring += 2*(1 - dissent); unemployed_spring += 2*(1-dissent); pro_republic += 2*(1-dissent)*(democratization-2) if democratization > 2

We are continuing to invest in our radio stations. Both commercial and political radio are becoming more prevalent, making it harder to spread our message amidst the competition.
